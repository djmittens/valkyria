; SSE Concurrency Stress Test (Short Version)
; Tests a few concurrent SSE connections

(load "src/prelude.valk")
(load "src/modules/test.valk")
(load "src/modules/aio/debug.valk")

(def {aio} (aio/await (aio/start)))

(fun {str-contains? haystack needle}
  {(> (len (str/split haystack needle)) 1)})

; SSE handler that sends a couple events then closes
(fun {stress-sse-handler req}
  {(= {path} (req/path req))
   (if (== path "/stress-sse")
     (do
       (= {stream} (sse/open req))
       (stream/write stream (str "event: diagnostics\ndata: " (aio/debug-diagnostics-state aio) "\n\n"))
       (aio/then (aio/sleep aio 100)
         (\ {_} {
           (stream/write stream (str "event: diagnostics-delta\ndata: " (aio/debug-diagnostics-delta aio) "\n\n"))
           (stream/close stream)})))
     {:status "404" :body "Not found"})})

(def {server} (http2/server-listen aio 0 stress-sse-handler))
(def {port} (http2/server-port server))

; Single SSE connection test
(fun {test-sse-connection}
  {(aio/then (http2/client-request aio "127.0.0.1" port "/stress-sse")
     (\ {resp}
       {(if (error? resp)
          false
          (str-contains? (http2/response-body resp) "event:"))}))})

(test/run-async aio (list
  (test-async "Single SSE connection receives events" (\ {} {
    (test-sse-connection)}))

  (test-async "Two concurrent SSE connections" (\ {} {
    (aio/then (aio/all (list (test-sse-connection) (test-sse-connection)))
      (\ {results} {(and (head results) (head (tail results)))}))}))

  (test-async "Three concurrent SSE connections" (\ {} {
    (aio/then (aio/all (list (test-sse-connection) (test-sse-connection) (test-sse-connection)))
      (\ {results} {(foldl (\ {acc r} {(and acc r)}) true results)}))}))
) {:suite-name "SSE Concurrency Stress Test" :timeout-ms 15000 :suite-timeout-ms 60000})
